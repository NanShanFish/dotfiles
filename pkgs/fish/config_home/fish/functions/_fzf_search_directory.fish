function _fzf_search_directory --description "Search the current directory. Replace the current token with the selected file paths."
    # Directly use fd binary to avoid output buffering delay caused by a fd alias, if any.
    # Debian-based distros install fd as fdfind and the fd package is something else, so
    # check for fdfind first. Fall back to "fd" for a clear error message.
    set -f fd_cmd (command -v fdfind || command -v fd)
    set -f --append fd_cmd --color=always "--follow" "--max-depth=5" "--hidden" --exclude '.git/'

    set -f fzf_arguments --scheme=path --multi --ansi --prompt="File> " $fzf_directory_opts
    set -f token (commandline --current-token)
    # expand any variables or leading tilde (~) in the token
    set -f expanded_token (eval echo -- $token)
    # unescape token because it's already quoted so backslashes will mess up the path
    set -f unescaped_exp_token (string unescape -- $expanded_token)

    # If the current token is a directory and has a trailing slash,
    # then use it as fd's base directory.
    # if string match --quiet -- "*/" $unescaped_exp_token && test -d "$unescaped_exp_token"
    if test -d "$unescaped_exp_token"
        if not string match --quiet -- "*/" $unescaped_exp_token
            set -f unescaped_exp_token $unescaped_exp_token"/"
        end
        set --append fd_cmd --base-directory=$unescaped_exp_token
        # use the directory name as fzf's prompt to indicate the search is limited to that directory
        set --prepend fzf_arguments --prompt="File $unescaped_exp_token> " --preview="_fzf_preview_file $expanded_token{}"
        set -f file_paths_selected $unescaped_exp_token($fd_cmd 2>/dev/null | _fzf_wrapper $fzf_arguments)
    else
        set -f token_dir "$(path dirname $unescaped_exp_token)"
        set -f token_file $(path basename $unescaped_exp_token)
        if test -d $token_dir
            set --prepend fzf_arguments --query="$token_file" --preview="_fzf_preview_file $token_dir/{}"
            set -f file_paths_selected "$token_dir/"($fd_cmd --base-directory=$token_dir 2>/dev/null | _fzf_wrapper $fzf_arguments)
        else if string match --quiet -- "/*" $unescaped_exp_token
            _fzf_complete
            return
        else
            set --prepend fzf_arguments --query="$unescaped_exp_token" --preview='_fzf_preview_file {}'
            set -f file_paths_selected ($fd_cmd 2>/dev/null | _fzf_wrapper $fzf_arguments)
        end
    end

    if test $status -eq 0
        commandline --current-token --replace -- (string escape -- $file_paths_selected | string join ' ')
    end

    commandline --function repaint
end
